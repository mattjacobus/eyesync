<!doctype html>
<html>
<head>
  <title>Eye</title>
  <meta charset="utf-8"/>
  <meta name="viewport" content="width=device-width, initial-scale=1"/>
  <link href="eye.css" rel="stylesheet" type="text/css"/>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/97/three.min.js"></script>
</head>
<script type="text/javascript">
  function openFullScreen() {
  var elem = document.getElementById("rendererdiv");
  if (elem.requestFullscreen) {
    elem.requestFullscreen();
  } else if (elem.mozRequestFullScreen) { /* Firefox */
    elem.mozRequestFullScreen();
  } else if (elem.webkitRequestFullscreen) { /* Chrome, Safari & Opera */
    console.log("going full screen");
    elem.webkitRequestFullscreen();
  } else if (elem.msRequestFullscreen) { /* IE/Edge */
    elem.msRequestFullscreen();
  }
}
</script>
<body>
  <!--<button onclick="openFullScreen();">Open Video in Fullscreen Mode</button>
  <div id="rendererdiv"></div>
  -->
<script id="vs" type="x-shader/x-vertex">
void main()	{
  gl_Position = vec4( position, 1.0 );
}
</script>
<script id="fs" type="x-shader/x-fragment">
precision highp float;

#define M_PI 3.1415926535897932384626433832795

uniform float time;
uniform vec2 mouse;
uniform vec2 vieworigin;
uniform vec2 resolution;

float rand(float n){return fract(sin(n) * 43758.5453123);}

float rand(vec2 n) { 
	return fract(sin(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
}

float noise(vec2 p){
	vec2 ip = floor(p);
	vec2 u = fract(p);
	u = u*u*(3.0-2.0*u);
	
	float res = mix(
		mix(rand(ip),rand(ip+vec2(1.0,0.0)),u.x),
		mix(rand(ip+vec2(0.0,1.0)),rand(ip+vec2(1.0,1.0)),u.x),u.y);
	return res*res;
}

float fbm(vec2 p) {
	float f = 0.0;
	f += 0.5000*noise(p); p*=2.02;
	f += 0.2500*noise(p); p*=2.03;
	f += 0.1250*noise(p); p*=2.01;
	f += 0.0625*noise(p); p*=2.04;
	f /= 0.9375;
	return f;
}

float fbm(float p) {
	return fbm(vec2(p, p*2.01));
}

float spike(float period, float thresh) {
	float bc = mod(time, period)/period;
	if (bc < thresh) {
		return sin(M_PI*bc/thresh);
	}
	return 0.0;
}

float look(float ltime, float tlen) {
	float cycle = float(int(ltime/tlen));
	float ctime = mod(ltime, tlen)/tlen;
	return mix(rand(cycle-1.0), rand(cycle), ctime);
}

void main(void) {
	vec2 q = (gl_FragCoord.xy+vieworigin.xy)/resolution.xy;
	vec2 p = -1.0 + 2.0*q;
	p.x *= resolution.x/resolution.y;
	float background = smoothstep(-0.25, 0.25, p.x);
	
	//p.x -= 0.75;

	// blink
	p.y *= 1.0 + 10.0*spike(10.0, 0.03);
	float r = sqrt(dot(p,p));
	float a = atan(p.y, p.x);
	
	// look
	vec2 lp = p;
	lp.x -= 0.5*(-0.5 + look(time, 2.0));
	lp.y -= 0.5*(-0.5 + look(time+23.0, 2.0));

	float lr = sqrt(dot(lp,lp));
	float la = atan(lp.y, lp.x);
	
	// breath
	float ss = 0.5 + 0.5*sin(time);
	float anim = 1.0 + 0.1*ss;	
	r *= anim;

	//vec3 col = mix(vec3(1.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0), fbm(time*0.01));
  vec3 col = vec3(0.0, 0.0, 0.0);
           
	if (r < 0.8) {
		// base color
		col = vec3(0.0, 0.3, 0.4);

		// cloudy
		float f = fbm(5.0*lp);
		col = mix(col, vec3(0.2, 0.5, 0.4), f);
		
		// yellow
		f = 1.0 - smoothstep(0.2, 0.5, lr);
		col = mix(col, vec3(0.9, 0.6, 0.2), f);
		
		a += 0.05*fbm(20.0*p);
		
		// white streaks
		f = smoothstep(0.3, 1.0, fbm(vec2(6.0*lr, 20.0*la)));
		col = mix(col, vec3(1.0), f);
		
		// black streaks
		f = smoothstep(0.3, 1.0, fbm(vec2(10.0*lr, 16.0*la)));
		col *= 1.0 - f;
		
		// curvature at edge
		f = smoothstep(0.6, 0.8, r);
		col *= 1.0 - 0.5*f;
		
		// pupil
		f = smoothstep(0.2, 0.25, lr);
		col *= f;
		
		// reflection
		f = 1.0 - smoothstep(0.0, 0.2, length(p - vec2(0.1, 0.1)));
		col += vec3(f);
		
		f = smoothstep(0.75, 0.8, r);
		col = mix(col, vec3(0.0), f);
	}
	gl_FragColor = vec4(col, 1.0);
}           
</script>
<script src="eye.js" type="text/javascript"> </script>
</body>
</html>
